#! /usr/bin/zsh

# pmenu
#   Manage suspended processes.
#
# Note:
#   This script must be run by sourcing the file,
#   otherwise it will not have access to the
#   shells suspended processes. 
#
# Example usage:
#   . ./pmenu resume

usage() {
  echo "Usage: pmenu <command>"
  echo ""
  echo "Commands:"
  echo "  resume    Resume a suspended job"
  echo "  kill      Kill a suspended job"
  echo ""
  echo "Note: This script must be sourced, not executed directly."
  return 1
}

command="$1"

if [[ -z $command ]]; then
  usage
  return 1
fi

case $command in
  resume | kill)
    job_array=()
    while read -r line; do
      if [[ $line =~ '^\[([0-9]+)\]\s+[+-]?\s+[a-z]+(\s+\([^)]+\))?\s+(.*)$' ]]; then
        num=$match[1]
        cmd=$match[3]
        job_array+=("$num $cmd")
      fi
    done < <(jobs)
    
    if [[ ${#job_array[@]} -eq 0 ]]; then
      echo "No suspended jobs"
      return 0
    fi

    selected=$(printf '%s\n' "${job_array[@]}" | gum choose \
      --header="$command job:" \
      --cursor.foreground="#98C379" \
      --selected.foreground="" \
      --item.foreground="" \
      --header.foreground="")
    
    if [[ -n $selected ]]; then
      num=${selected%% *}
      if [[ $command == "resume" ]]; then
        fg %$num
      else
        kill -kill %$num
      fi
    else
      return 1
    fi
    ;;
  *)
    usage
    return 1
    ;;
esac
